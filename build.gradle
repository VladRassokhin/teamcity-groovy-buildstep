buildscript {
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.rodm:gradle-teamcity-plugin:0.8.1"
    }
}

repositories {
    jcenter()
}

ext.teamCityVersion = '8.1.5'


allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = '1.0.0-SNAPSHOT'

}

idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

subprojects {


    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    group = 'me.champeau.teamcity'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile 'log4j:log4j:1.2.17'
    }

    idea.module {
        downloadSources = true
        downloadJavadoc = false
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }


    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

}


/**
 * Gradle subproject with agent-side sources of the plugin.
 */
project(':agent') {
    apply plugin: 'com.github.rodm.teamcity-agent'
    apply plugin: 'groovy'
    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.3'
        compile 'org.apache.ivy:ivy:2.4.0-rc1'
    }
    teamcity {
        version = teamCityVersion

        descriptor {
            pluginDeployment {
                useSeparateClassloader = true
            }
        }
    }
}

/**
 * Gradle subproject with server-side sources of the plugin.
 */
project(':server') {
    apply plugin: 'com.github.rodm.teamcity-server'
    dependencies {
        agent project(path: ':agent', configuration: 'plugin')
    }
    teamcity {
        version = teamCityVersion

        server {
            descriptor {
                name = 'groovy-buildstep'
                displayName = 'Groovy build step plugin'
                version = project.version
                vendorName = 'CÃ©dric Champeau'
                vendorUrl = 'http://melix.github.io/blog'
                description = 'Allows to execute groovy scripts as build steps'
                email = 'cedric.champeau@gmail.com'
                useSeparateClassloader = true
            }
        }
    }
}
